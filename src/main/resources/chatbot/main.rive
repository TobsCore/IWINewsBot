! version = 2.0

//timetable
+ welche (@lectures) habe ich [am] *
- <call>timetable <star2></call>

+ welche (@lectures) habe ich
- <call>timetable</call>

+ (wie sieht|was ist) mein stundenplan [aus]
- <call>timetable</call>

+ wie sieht mein tag aus
- <call>timetable heute</call>

+ wie sieht mein tag morgen aus
- <call>timetable morgen</call>

+ wie sieht mein morgiger tag aus
- <call>timetable morgen</call>

+ was habe ich [am] _ [fÃ¼r @lectures]
- <call>timetable <star></call>

+ was steht [am] _ an
- <call>timetable <star></call>

+ was steht an
- <call>timetable</call>


//where
+ wo habe ich *
- <call>spaceandtime <star></call>

+ wo findet [die] [@lectures] * statt
- <call>spaceandtime <star></call>

+ in welchem raum findet [die] [@lectures] * statt
- <call>spaceandtime <star></call>

+ in welchem raum ist *
- <call>spaceandtime <star></call>

+ in welchem raum findet * statt
- <call>spaceandtime <star></call>


//when
+ wann findet [die] [@lectures] * statt
- <call>spaceandtime <star></call>

+ wann habe ich *
- <call>spaceandtime <star></call>

+ wann ist *
- <call>spaceandtime <star></call>


//mensa
+ was (gibt|gab) es (in der mensa|zu essen)
- <call>mensa</call>

+ was (gibt|gab) es [am] * [in der mensa|zu essen]
- <call>mensa <star2></call>

+ [welche|welches] * @gerichte (gibt|gab) es [in der mensa|zu essen]
- <call>mensa <star1></call>

+ [welche|welches] * @gerichte (gibt|gab) es [am] * [in der mensa|zu essen]
- <call>mensa <star1> <star3></call>

+ [welche|welches] @gerichte * (gibt|gab) es [am] * [in der mensa|zu essen]
- <call>mensa <star1> <star3></call>

+ [welche|welches] @gerichte (ohne|mit) * (gibt|gab) es [am] * [in der mensa|zu essen]
- <call>mensa <star1> <star2> <star4></call>

+ [welche|welches] @gerichte * (gibt|gab) es [in der mensa|zu essen]
- <call>mensa <star1></call>

+ [welche|welches] @gerichte (ist|war|enthÃ¤lt|sind|waren|enthalten) * [in der mensa|zu essen]
- <call>mensa <star2></call>

+ [welche|welches] @gerichte (ist|war|enthÃ¤lt|sind|waren|enthalten) (keine|kein) * [in der mensa|zu essen]
- <call>mensa ohne <star3></call>

+ [welche|welches] @gerichte (ist|war|enthÃ¤lt|sind|waren|enthalten) (keine|kein) * und * [in der mensa|zu essen]
- <call>mensa ohne <star3> und ohne <star4></call>

+ [welche|welches] @gerichte (gibt|gab) es * [in der mensa|zu essen]
- <call>mensa <star2></call>

+ ich habe hunger
- Vielleicht gibt es ja in der Mensa etwas Gutes? ğŸ˜‰\n
^ <call>mensa</call>

+ ich habe (hunger|schmacht|lust) [auf|nach] *
- Vielleicht gibt es ja in der Mensa etwas Gutes? ğŸ˜‰\n
^ <call>mensa <star2></call>

+ ich habe (keinen|keine) (hunger|schmacht|lust) [auf|nach] *
- Vielleicht gibt es ja in der Mensa trotzdem etwas Gutes? ğŸ˜‰\n
^ <call>mensa ohne <star3></call>

+ ich habe (keinen|keine) (hunger|schmacht|lust) [auf|nach] * und *
- Vielleicht gibt es ja in der Mensa trotzdem etwas Gutes? ğŸ˜‰\n
^ <call>mensa ohne <star3> und ohne <star4></call>

+ ich (will|mÃ¶chte) essen
- Vielleicht gibt es ja in der Mensa etwas Gutes? ğŸ˜‰\n
^ <call>mensa</call>

+ ich (will|mÃ¶chte) * essen
- <call>mensa <star2></call>

+ ich (will|mÃ¶chte) kein * essen
- <call>mensa ohne <star2></call>

+ ich bin [ein|eine] (vegetarier|vegetarierin)
- Oh, wie schÃ¶n! Das gibt es dazu in der Mensa: \n
^ <call>mensa vegetarisch und vegan</call>

+ ich bin [ein|eine] (veganer|veganerin)
- Oh, wie schÃ¶n! Das gibt es dazu in der Mensa: \n
^ <call>mensa vegan</call>

+ ich ernÃ¤hre mich _
- Oh, wie schÃ¶n! Das gibt es dazu in der Mensa: \n
^ <call>mensa <star></call>


//profs
+ [wie lautet|was ist] die (@mail) von [@nametitles] *
- <call>profs <star2></call>

+ in welchem (raum|zimmer) (finde ich|ist|sitzt) [@nametitles] *
- <call>profs <star3></call>

+ (wie|wo) finde ich [@nametitles] *
- <call>profs <star2></call>

+ wo ist [@nametitles] *
- <call>profs <star></call>

+ wann hat [@nametitles] * (sprechstunde|zeit)
- <call>profs <star></call>

+ [wann|was] sind die (sprechzeiten|sprechstunden) von [@nametitles] *
- <call>profs <star2></call>

+ [wann|was] ist die (sprechzeit|sprechstunde) von [@nametitles] *
- <call>profs <star2></call>

+ was sind die kontaktdaten von [@nametitles] *
- <call>profs <star></call>

+ wie kann ich [@nametitles] * (kontaktieren|erreichen)
- <call>profs <star1></call>

+ was weiÃŸt du (zu den|Ã¼ber die) (lehrenden|professoren|dozenten)
- Zu welchem Lehrenden mÃ¶chtest du etwas wissen? Gib bitte den Namen mit Anrede ein.

+ kannst du mir [etwas|was] zu den (lehrenden|professoren|dozenten) sagen
- Zu welchem Lehrenden mÃ¶chtest du etwas wissen? Gib bitte den Namen mit Anrede ein.

+ [@nametitles] *
% zu welchem lehrenden mÃ¶chtest du etwas wissen gib bitte den namen mit anrede ein
- <call>profs <star></call>

% zu welchem lehrenden mÃ¶chtest du etwas wissen gib bitte den namen mit anrede ein
- <call>profs <star2></call>

+ was unterrichtet [@nametitles] *?
- <call>profslectures <star></call>

+ welche [@lectures] hÃ¤lt [@nametitles] *?
- <call>profslectures <star></call>

+ welche [@lectures] (werden|wurden|sind) von [@nametitles] * gehalten?
- <call>profslectures <star3></call>


//If no match is found, call no-comprendo routine. It will take care of that.
+ *
- <call>no-comprendo</call>